plugins {
  id 'jacoco'
  id 'java-library'
// Somehow applying Spotless plugin fails. Related to diffplug/spotless#860??
//id 'com.diffplug.spotless' version '6.0.0'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'com.github.johnrengelman.shadow' version '7.1.0'
  id 'ru.vyarus.quality' version '4.7.0'
}

group = 'com.github.tueda'
archivesBaseName = 'donuts-python'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def versions = [
  checkstyle: '9.1',
  junit: '5.8.1',
  lombok: '1.18.22',
  pmd: '6.40.0',
  spotbugs: '4.4.2',
  truth: '1.1.3',
]

repositories {
  mavenCentral()
}

dependencies {
  implementation project(':donuts-java')
  compileOnly "org.projectlombok:lombok:${versions.lombok}"
  annotationProcessor "org.projectlombok:lombok:${versions.lombok}"
  testImplementation "com.google.truth:truth:${versions.truth}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
  // for dependencyUpdates
  testRuntimeOnly "net.sourceforge.pmd:pmd:${versions.pmd}"
}

test {
  useJUnitPlatform()
}

// spotless {
//   java {
//     googleJavaFormat('1.7')
//   }
//   groovyGradle {
//     greclipse().configFile('config/greclipse.properties')
//   }
// }

configurations {
  checkstyleConfig
}

dependencies {
  checkstyleConfig("com.puppycrawl.tools:checkstyle:${versions.checkstyle}") {
    transitive = false
  }
}

quality {
  checkstyleVersion = versions.checkstyle
  pmdVersion = versions.pmd
  spotbugsVersion = versions.spotbugs

  spotbugsEffort = 'max'
  spotbugsLevel = 'low'
}

afterEvaluate {
  checkstyle {
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  }
  pmd {
    incrementalAnalysis = true
    ruleSetFiles = files('config/pmd.xml')
  }
}

shadowJar {
  minimize()
}
